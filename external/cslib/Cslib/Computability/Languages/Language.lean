/-
Copyright (c) 2025 Ching-Tsun Chou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ching-Tsun Chou
-/

module

public import Cslib.Init
public import Mathlib.Computability.Language

/-!
# Language (additional definitions and theorems)

This file contains additional definitions and theorems about `Language`
as defined and developed in `Mathlib.Computability.Language`.
-/

@[expose] public section

namespace List

variable {α : Type*}

/-- `[]` is the only list over an empty type. -/
theorem eq_nil_ofIsEmpty [IsEmpty α] (xl : List α) : xl = [] := by
  have hu := List.uniqueOfIsEmpty (α := α)
  simp [Unique.eq_default]

end List

namespace Language

open Set List
open scoped Computability

variable {α : Type*} {l m : Language α}

/-- `0` and `1` are the only possible languages over an empty type. -/
theorem eq_zero_or_one_ofIsEmpty [IsEmpty α] (l : Language α) : l = 0 ∨ l = 1 := by
  by_cases h : l = 0
  · simp [h]
  · right
    ext xl
    obtain ⟨yl, _⟩ := nonempty_iff_ne_empty.mpr h
    obtain ⟨rfl⟩ := eq_nil_ofIsEmpty xl
    obtain ⟨rfl⟩ := eq_nil_ofIsEmpty yl
    simpa

@[simp]
theorem mem_biInf {I : Type*} (s : Set I) (l : I → Language α) (x : List α) :
    (x ∈ ⨅ i ∈ s, l i) ↔ ∀ i ∈ s, x ∈ l i :=
  mem_iInter₂

#adaptation_note
/-- A grind regression found moving to nightly-2026-03-31 (changes from lean#13166) -/
@[simp]
theorem mem_biSup {I : Type*} (s : Set I) (l : I → Language α) (x : List α) :
    (x ∈ ⨆ i ∈ s, l i) ↔ ∃ i ∈ s, x ∈ l i where
  mp h := bex_def.mp (mem_iUnion₂.mp h)
  mpr h :=  mem_iUnion₂.mpr (bex_def.mpr h)

theorem le_one_iff_eq : l ≤ 1 ↔ l = 0 ∨ l = 1 :=
  subset_singleton_iff_eq

@[simp, scoped grind =]
theorem mem_singleton (x y : List α) : x ∈ ({y} : Language α) ↔ x = y :=
  Iff.rfl

@[simp, scoped grind =]
theorem mem_sub_one (x : List α) : x ∈ (l - 1) ↔ x ∈ l ∧ x ≠ [] :=
  Iff.rfl

@[simp, scoped grind =]
theorem reverse_sub (l m : Language α) : (l - m).reverse = l.reverse - m.reverse := by
  ext x; simp [mem_sub]

#adaptation_note
/-- A grind regression found moving to nightly-2026-03-31 (changes from lean#13166) -/
@[scoped grind =]
theorem sub_one_mul : (l - 1) * l = l * l - 1 := by
  ext x; constructor
  · rintro ⟨u, h_u, v, h_v, rfl⟩
    constructor
    · exact ⟨u, Set.mem_of_mem_inter_left h_u, v, h_v, rfl⟩
    · by_contra h
      have := mem_sub_one u |>.mp h_u
      have := mem_one (u ++ v) |>.mp h
      grind [append_eq_nil_iff]
  · rintro ⟨⟨u, h_u, v, h_v, rfl⟩, h_x⟩
    rcases eq_or_ne u [] with (rfl | h_u')
    · use v, (mem_sub l 1 v |>.mpr) ⟨h_v, Not.intro h_x⟩, []
      grind [mem_sub, mem_one]
    · use u, (mem_sub_one u).mpr ⟨h_u, h_u'⟩, v

@[scoped grind =]
theorem mul_sub_one : l * (l - 1) = l * l - 1 := by
  calc
    _ = (l * (l - 1)).reverse.reverse := by rw [reverse_reverse]
    _ = ((l.reverse - 1) * l.reverse).reverse := by rw [reverse_mul, reverse_sub, reverse_one]
    _ = (l.reverse * l.reverse - 1).reverse := by rw [sub_one_mul]
    _ = _ := by rw [reverse_sub, reverse_one, reverse_mul, reverse_reverse]

#adaptation_note
/-- A grind regression found moving to nightly-2026-03-31 (changes from lean#13166) -/
@[scoped grind =]
theorem kstar_sub_one : l∗ - 1 = (l - 1) * l∗ := by
  ext x; constructor
  · rintro ⟨h1, h2⟩
    obtain ⟨xl, rfl, h_xl⟩ := kstar_def_nonempty l ▸ h1
    have h3 : ¬ xl = [] := by grind [one_def]
    obtain ⟨x, xl', h_xl'⟩ := exists_cons_of_ne_nil h3
    subst h_xl'
    refine ⟨x, mem_preimage.mp (h_xl x ?_), xl'.flatten, join_mem_kstar ?_, ?_⟩ <;> grind
  · rintro ⟨y, ⟨h_y, h_1⟩, z, h_z, rfl⟩
    refine ⟨?_, ?_⟩
    · apply (show l * l∗ ≤ l∗ by exact mul_kstar_le_kstar)
      exact ⟨y, h_y, z, h_z, rfl⟩
    · grind [one_def, append_eq_nil_iff]

@[scoped grind =]
theorem sub_one_kstar : (l - 1)∗ = l∗ := by
  ext x
  grind [mem_kstar, mem_kstar_iff_exists_nonempty]

@[scoped grind .]
theorem kstar_iff_mul_add : m = l∗ ↔ m = (l - 1) * m + 1 := by
  rw [self_eq_mul_add_iff, mul_one, sub_one_kstar]
  grind

end Language
