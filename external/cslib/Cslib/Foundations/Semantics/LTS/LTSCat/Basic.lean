/-
Copyright (c) 2026 Ayberk Tosun (Zeroth Research). All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ayberk Tosun
-/

module

public import Mathlib.CategoryTheory.Category.Basic
public import Cslib.Foundations.Semantics.LTS.Basic
public import Mathlib.Control.Basic

/-! # Category of Labelled Transition Systems

This file contains the definition of the category of labelled transition systems
as defined in Winskel and Nielsen's handbook chapter [WinskelNielsen1995].

## References

* [N. Winskel and M. Nielsen, *Models for concurrency*][WinskelNielsen1995]
-/

@[expose] public section

namespace Cslib

variable {State Label : Type*}

/--
We first define what is denoted Tran* in [WinskelNielsen1995]: the extension of
a transition relation with idle transitions.
-/
def LTS.withIdle (lts : LTS State Label) : LTS State (Option Label) :=
  ⟨fun s l s' => l.elim (s = s') (lts.Tr s · s')⟩

/-! ## LTSs and LTS morphisms form a category -/

set_option linter.checkUnivs false in
/--
The definition of labelled transition system (with the type of states and the
type of labels as part of the structure).
-/
structure LTSCat : Type (max u v + 1) where
  /-- Type of states of an LTS -/
  State : Type u
  /-- Type of labels of an LTS -/
  Label : Type v
  /-- Transition relation of an LTS -/
  lts : LTS State Label

/--
A morphism between two labelled transition systems consists of (1) a function on
states, (2) a partial function on labels, and a proof that (1) preserves each
transition along (2).
-/
structure LTS.Morphism (lts₁ lts₂ : LTSCat) : Type where
  /-- Mapping of states of `lts₁` to states of `lts₂` -/
  stateMap : lts₁.State → lts₂.State
  /-- Mapping of labels of `lts₁` to labels of `lts₂` -/
  labelMap : lts₁.Label → Option lts₂.Label
  /-- Stipulation that `stateMap` preserve transitions -/
  labelMap_tr (s s' : lts₁.State) (l : lts₁.Label) :
    lts₁.lts.Tr s l s' → (withIdle lts₂.lts).Tr (stateMap s) (labelMap l) (stateMap s')

/-- The identity LTS morphism. -/
def LTS.Morphism.id (lts : LTSCat) : LTS.Morphism lts lts where
  stateMap := _root_.id
  labelMap := pure
  labelMap_tr _ _ _ := _root_.id

/-- Composition of LTS morphisms.

We use Kleisli composition to define this.
-/
def LTS.Morphism.comp {lts₁ lts₂ lts₃} (f : LTS.Morphism lts₁ lts₂) (g : LTS.Morphism lts₂ lts₃) :
    LTS.Morphism lts₁ lts₃ where
  stateMap := g.stateMap ∘ f.stateMap
  labelMap := f.labelMap >=> g.labelMap
  labelMap_tr s s' l h := by
    obtain ⟨f, μ, p⟩ := f
    obtain ⟨g, ν, q⟩ := g
    simp only [LTS.withIdle] at p q
    change ((μ l).bind ν).elim (g (f s) = g (f s')) _
    cases hμ : μ l with grind

/-- Finally, we prove that these form a category. -/
instance : CategoryTheory.Category LTSCat where
  Hom := LTS.Morphism
  id := LTS.Morphism.id
  comp := LTS.Morphism.comp
  comp_id _ := by
    simp only [LTS.Morphism.comp, LTS.Morphism.id]
    congr 1
    rw [fish_pure]
  assoc _ _ _ := by
    simp only [LTS.Morphism.comp]
    congr 1
    rw [fish_assoc]

end Cslib
